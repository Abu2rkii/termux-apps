apply plugin: 'com.android.application'

android {
    namespace "com.termux.api"

    def appVersionName = System.getenv("TERMUX_API_APP_VERSION_NAME") ?: ""
    def apkVersionTag = System.getenv("TERMUX_API_APK_VERSION_TAG") ?: ""

    defaultConfig {
        compileSdk project.properties.compileSdkVersion.toInteger()
        minSdkVersion project.properties.minSdkVersion.toInteger()
        targetSdkVersion project.properties.targetSdkVersion.toInteger()
        versionCode 51
        versionName "0.50.1"

        manifestPlaceholders.TERMUX_PACKAGE_NAME = "com.termux"
        manifestPlaceholders.TERMUX_APP_NAME = "Termux"
        manifestPlaceholders.TERMUX_API_APP_NAME = "Termux:API"
    }

    signingConfigs {
        debug {
            storeFile file('testkey_untrusted.jks')
            keyAlias 'alias'
            storePassword 'xrj45yWGLbsO7W0v'
            keyPassword 'xrj45yWGLbsO7W0v'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false // Reproducible builds
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "debug") {
                outputFileName = new File("termux-api_" + (apkVersionTag ? apkVersionTag : "debug") + ".apk")
            } else if (variant.buildType.name == "release") {
                outputFileName = new File("termux-api_" + (apkVersionTag ? apkVersionTag : "release") + ".apk")
            }
        }
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.biometric:biometric:1.2.0-alpha05'
    implementation 'androidx.media:media:1.6.0'
}

task versionName {
    doLast {
        print android.defaultConfig.versionName
    }
}
